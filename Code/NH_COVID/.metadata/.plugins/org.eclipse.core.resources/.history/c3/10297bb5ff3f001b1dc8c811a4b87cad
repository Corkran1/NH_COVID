/**
* Name: ImmuneResponse
* Model to test the immune response equation
* Author: jpablo91
* Tags: 
*/


model ImmuneResponse

global {
	float Vi_first_dose <- 0.6 parameter: "first dose";
	float Vi_second_dose <- 0.9;
	int Immunity_decay_t <- 110;
	int Revaccination_t <- 21;
	float G_Rise <- 0.2;
	float G_Decay <- 0.001;
	init {
		create Population_sp number: 1;
	}
	
	reflex EndSim when: cycle = 250{
		do pause;
	}
}

//Species which represent the Ordinary Differential Equations System 
species Population_sp {
	float t; //Variable to represent the discrete time for integration
   	float MVi <- Vi_first_dose; //Max Effect
	float Vi <- 0.01; //initial Immune response
	float dVi <- MVi - Vi; // Immunity to Max
	
  	float beta <- G_Rise; //Rate of rise
   	float gamma <- G_Decay; //Rate of decay
   		
   	float h <- 0.1;
   	string mm <- "Euler";

	// Parameters must follow exact order S, I, R, t  and N,beta,gamma	
	equation eqSIR{ 
    diff(dVi,t) = (- beta * dVi * Vi / MVi);
    diff(Vi,t) = (beta * dVi * Vi / MVi) - (gamma * Vi);
    }

	reflex solving {
		solve eqSIR method: mm step_size:h ;
	}
	
	reflex Revaccination{
		if(cycle = Revaccination_t){
			MVi <- Vi_second_dose;
			dVi <- MVi -Vi;
		}
		if(cycle = Immunity_decay_t){
			gamma <- 0.05;
		}
	}
}


experiment mysimulation type: gui {
	output {	
		display display_charts {
			species Population_sp;
			chart 'SIR_agent' type: series background: #white {
//				data "S" value: first(predefined_ODE_SIR_agent).S color: #green;
				data "I" value: first(Population_sp).Vi color: #red;
//				data "R" value: first(predefined_ODE_SIR_agent).R color: #blue;
			}
		}
	}
}